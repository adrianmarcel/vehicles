plugins {
    id 'java'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '6.5.1'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'br.com.fiap.service.vehicles'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
    java {
        googleJavaFormat('1.22.0')
        target 'src/**/*.java'
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/core/config/**',
                    '**/core/domain/**',
                    '**/Application.java'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: [
                    '**/core/config/**',
                    '**/core/domain/**',
                    '**/Application.java'
            ])
        }))
    }
}

springBoot {
    mainClass = 'br.com.fiap.service.vehicles.Application'
    buildInfo()
}
